plugins {
    id 'checkstyle'
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'io.freefair.lombok' version '6.6.1'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = "Gegy's Maven"
        url = "https://maven.gegy.dev/"
    }

    maven {
        name = "Open-Collab Snapshots"
        url = "https://repo.opencollab.dev/maven-snapshots/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modCompileOnly("supercoder79:databreaker:${databreaker_version}") {
        exclude module: "fabric-loader"
    }

    testImplementation "org.junit.jupiter:junit-jupiter:5.7.1"

    /** include() shades the dependency in the mod jar.
     * NOTE: You must include EVERY dependency or else it won't launch!
     **/
    include(implementation("com.github.steveice10:opennbt:1.5-SNAPSHOT"))
    include(modImplementation("net.kyori:adventure-platform-fabric:5.6.1"))
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

assemble {
    dependsOn(checkstyleMain)
}

test {
    useJUnitPlatform()
}

jar {
    from("../LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}