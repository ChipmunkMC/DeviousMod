plugins {
    id("java")
    id("checkstyle")
    id("fabric-loom") version "1.0-SNAPSHOT"
}

java.sourceCompatibility = JavaVersion.VERSION_17

project.version = project.mod_version
project.group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/deviousmod.accesswidener")
}

repositories {
    mavenCentral()

    maven {
        name = "OpenCollab"
        url = "https://repo.opencollab.dev/maven-snapshots/"
    }

    maven {
        name = "Jitpack"
        url = "https://jitpack.io/"
    }
}

configurations {
    implementation.extendsFrom require
    api.extendsFrom require
}

dependencies {
    minecraft(libs.minecraft)
    mappings(libs.yarn.mappings)
    modImplementation(libs.fabric.loader)
    modImplementation(libs.fabric.api)

    require(libs.opennbt)
    require(libs.reflector)

    modImplementation(libs.adventure.platform.fabric)
}

tasks {
    assemble {
        dependsOn(checkstyleMain)
    }

    compileJava {
        options.release.set(17)
    }

    processResources {
        inputs.property("version", project.version)

        filesMatching("fabric.mod.json") {
            expand(project.properties)
        }
    }

    jar {
        dependsOn configurations.require
        from {
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
            configurations.require.collect {
                zipTree(it)
            }
        }

        from("LICENSE") {
            rename { "LICENSE_${archiveBaseName.get()}" }
        }
    }
}